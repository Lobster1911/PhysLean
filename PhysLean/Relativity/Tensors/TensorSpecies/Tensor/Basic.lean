/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Relativity.Tensors.TensorSpecies.Contractions.ContrMap
/-!

# Tensors associated with a tensor species

-/

open IndexNotation
open CategoryTheory
open MonoidalCategory

namespace TensorSpecies
open OverColor

variable {k : Type} [CommRing k] (S : TensorSpecies k)

/-- The tensors associated with a list of indicies of a given color
  `c : Fin n ‚Üí S.C`. s-/
noncomputable abbrev Tensor {n : ‚Ñï} (c : Fin n ‚Üí S.C) : Type := (S.F.obj (OverColor.mk c))

namespace Tensor

variable {S : TensorSpecies k} {n n' n2 : ‚Ñï} {c : Fin n ‚Üí S.C} {c' : Fin n' ‚Üí S.C}
  {c2 : Fin n2 ‚Üí S.C}

/-- Given a list of indices `c : Fin n ‚Üí S.C` e.g. `![.up, .down]`, the type
  `ComponentIdx c` is the type of components indexes of a tensor with those indices
  e.g. `‚ü®0, 2‚ü©` corresponding to `T‚Å∞‚ÇÇ`. -/
abbrev ComponentIdx {n : ‚Ñï} (c : Fin n ‚Üí S.C) : Type := Œ† j, Fin (S.repDim (c j))

lemma ComponentIdx.congr_right {n : ‚Ñï} {c : Fin n ‚Üí S.C} (b : ComponentIdx c)
    (i j : Fin n) (h : i = j) : b i = Fin.cast (by simp [h]) (b j) := by
  subst h
  rfl

/-!

## Pure tensors

-/

/-- The type of pure tensors associated to a list of indices `c : OverColor S.C`.
  A pure tensor is a tensor which can be written in the form `v1 ‚äó‚Çú v2 ‚äó‚Çú v3 ‚Ä¶`. -/
abbrev Pure (S : TensorSpecies k) (c : Fin n ‚Üí S.C) : Type :=
    (i : Fin n) ‚Üí S.FD.obj (Discrete.mk (c i))

namespace Pure

@[simp]
lemma congr_right {n : ‚Ñï} {c : Fin n ‚Üí S.C} (p : Pure S c)
    (i j : Fin n) (h : i = j) : S.FD.map (eqToHom (by rw [h])) (p j) = p i := by
  subst h
  simp

lemma congr_mid {n : ‚Ñï} {c : Fin n ‚Üí S.C} (c' : S.C) (p : Pure S c)
    (i j : Fin n) (h : i = j) (hi : c i = c') (hj : c j = c') :
    S.FD.map (eqToHom (by rw [hi] : { as := c i } = { as := c' })) (p i) =
    S.FD.map (eqToHom (by rw [hj] : { as := c j } = { as := c' })) (p j) := by
  subst hi
  simp only [eqToHom_refl, Discrete.functor_map_id, ConcreteCategory.id_apply]
  symm
  apply congr_right
  exact h

lemma map_map_apply {n : ‚Ñï} {c : Fin n ‚Üí S.C} (c1 c2 : S.C) (p : Pure S c) (i : Fin n)
    (f : ({ as := c i } : Discrete S.C) ‚ü∂ { as := c1 })
    (g : ({ as := c1 } : Discrete S.C) ‚ü∂ { as := c2 }) :
    (ConcreteCategory.hom (S.FD.map g))
    ((ConcreteCategory.hom (S.FD.map f)) (p i)) =
    S.FD.map (f ‚â´ g) (p i) := by
  simp only [Functor.map_comp, ConcreteCategory.comp_apply]

/-- The tensor correpsonding to a pure tensor. -/
noncomputable def toTensor {n : ‚Ñï} {c : Fin n ‚Üí S.C} (p : Pure S c) : S.Tensor c :=
  PiTensorProduct.tprod k p

lemma toTensor_apply {n : ‚Ñï} (c : Fin n ‚Üí S.C) (p : Pure S c) :
    toTensor p = PiTensorProduct.tprod k p := rfl

/-- Given a list of indices `c` of `n` indices, a pure tensor `p`, an element `i : Fin n` and
  a `x` in `S.FD.obj (Discrete.mk (c i))` then `update p i x` corresponds to `p` where
  the `i`th part of `p` is replaced with `x`.

  E.g. if `n = 2` and `p = v‚ÇÄ ‚äó‚Çú v‚ÇÅ` then `update p 0 x = x ‚äó‚Çú v‚ÇÅ`. -/
def update {n : ‚Ñï} {c : Fin n ‚Üí S.C} [inst : DecidableEq (Fin n)] (p : Pure S c) (i : Fin n)
    (x : S.FD.obj (Discrete.mk (c i))) : Pure S c := Function.update p i x

@[simp]
lemma update_same {n : ‚Ñï} {c : Fin n ‚Üí S.C} [inst : DecidableEq (Fin n)] (p : Pure S c) (i : Fin n)
    (x : S.FD.obj (Discrete.mk (c i))) : (update p i x) i = x := by
  simp [update]

@[simp]
lemma update_succAbove_apply {n : ‚Ñï} {c : Fin (n + 1) ‚Üí S.C} [inst : DecidableEq (Fin (n + 1))]
    (p : Pure S c) (i : Fin (n + 1)) (j : Fin n) (x : S.FD.obj (Discrete.mk (c (i.succAbove j)))) :
    update p (i.succAbove j) x i = p i := by
  simp only [update]
  rw [Function.update_of_ne]
  exact Fin.ne_succAbove i j

@[simp]
lemma toTensor_update_add {n : ‚Ñï} {c : Fin n ‚Üí S.C} [inst : DecidableEq (Fin n)] (p : Pure S c)
    (i : Fin n) (x y : S.FD.obj (Discrete.mk (c i))) :
    (update p i (x + y)).toTensor = (update p i x).toTensor + (update p i y).toTensor := by
  simp [toTensor, update]

@[simp]
lemma toTensor_update_smul {n : ‚Ñï} {c : Fin n ‚Üí S.C} [inst : DecidableEq (Fin n)] (p : Pure S c)
    (i : Fin n) (r : k) (y : S.FD.obj (Discrete.mk (c i))) :
    (update p i (r ‚Ä¢ y)).toTensor = r ‚Ä¢ (update p i y).toTensor := by
  simp [toTensor, update]

/-- Given a list of indices `c` of length `n + 1`, a pure tensor `p` and an `i : Fin (n + 1)`, then
  `drop p i` is the tensor `p` with it's `i`th part dropped.

  For example, if `n = 2` and `p = v‚ÇÄ ‚äó‚Çú v‚ÇÅ ‚äó‚Çú v‚ÇÇ` then `drop p 1 = v‚ÇÄ ‚äó‚Çú v‚ÇÇ`. -/
def drop {n : ‚Ñï} {c : Fin (n + 1) ‚Üí S.C} (p : Pure S c) (i : Fin (n + 1)) :
    Pure S (c ‚àò i.succAbove) :=
  fun j => p (i.succAbove j)

@[simp]
lemma update_succAbove_drop {n : ‚Ñï} {c : Fin (n + 1) ‚Üí S.C} [inst : DecidableEq (Fin (n + 1))]
    (p : Pure S c) (i : Fin (n + 1)) (k : Fin n) (x : S.FD.obj (Discrete.mk (c (i.succAbove k)))) :
    (update p (i.succAbove k) x).drop i = (p.drop i).update k x := by
  ext j
  simp only [Function.comp_apply, drop, update]
  by_cases h : j = k
  ¬∑ subst h
    simp
  ¬∑ rw [Function.update_of_ne h, Function.update_of_ne]
    ¬∑ rfl
    ¬∑ simp only [ne_eq]
      rw [Function.Injective.eq_iff (Fin.succAbove_right_injective (p := i))]
      exact h

@[simp]
lemma update_drop_self {n : ‚Ñï} {c : Fin (n + 1) ‚Üí S.C} [inst : DecidableEq (Fin (n + 1))]
    (p : Pure S c) (i : Fin (n + 1)) (x : S.FD.obj (Discrete.mk (c i))) :
    (update p i x).drop i = p.drop i := by
  ext k
  simp only [Function.comp_apply, drop, update]
  rw [Function.update_of_ne]
  exact Fin.succAbove_ne i k

lemma Œº_toTensor_tmul_toTensor {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (t : Pure S c) (t1 : Pure S c1) :
    ((Functor.LaxMonoidal.Œº S.F _ _).hom (t.toTensor ‚äó‚Çú t1.toTensor)) =
    PiTensorProduct.tprod k (fun | Sum.inl i => t i | Sum.inr i => t1 i) := by
  change lift.ŒºModEquiv _ _ _ (t.toTensor ‚äó‚Çú t1.toTensor) = _
  rw [lift.ŒºModEquiv]
  simp only [lift.objObj'_V_carrier, OverColor.instMonoidalCategoryStruct_tensorObj_left,
    OverColor.instMonoidalCategoryStruct_tensorObj_hom, Action.instMonoidalCategory_tensorObj_V,
    Functor.id_obj, Equivalence.symm_inverse, Action.functorCategoryEquivalence_functor,
    Action.FunctorCategoryEquivalence.functor_obj_obj]
  rw [LinearEquiv.trans_apply]
  rw [toTensor, toTensor]
  rw [PhysLean.PiTensorProduct.tmulEquiv_tmul_tprod]
  simp only [PiTensorProduct.congr_tprod]
  congr
  funext i
  match i with
  | Sum.inl i =>
    rfl
  | Sum.inr i =>
    rfl

/-!

## Components

-/

/-- Given an element `b` of `ComponentIdx c` and a pure tensor `p` then
  `component p b` is the element of the ring `k` correpsonding to
  the component of `p` in the direction `b`.

  For example, if `p = v ‚äó‚Çú w` and `b = ‚ü®0, 1‚ü©` then `component p b = v‚Å∞ ‚äó‚Çú w¬π`. -/
def component {n : ‚Ñï} {c : Fin n ‚Üí S.C} (p : Pure S c) (b : ComponentIdx c) : k :=
    ‚àè i, (S.basis (c i)).repr (p i) (b i)

lemma component_eq {n : ‚Ñï} {c : Fin n ‚Üí S.C} (p : Pure S c) (b : ComponentIdx c) :
    p.component b = ‚àè i, (S.basis (c i)).repr (p i) (b i) := by rfl

lemma component_eq_drop {n : ‚Ñï} {c : Fin (n + 1) ‚Üí S.C} (p : Pure S c) (i : Fin (n + 1))
    (b : ComponentIdx c) :
    p.component b = ((S.basis (c i)).repr (p i) (b i)) *
    ((drop p i).component (fun j => b (i.succAbove j))) := by
  simp only [component, Nat.succ_eq_add_one, Function.comp_apply]
  rw [Fin.prod_univ_succAbove _ i]
  rfl

@[simp]
lemma component_update_add {n : ‚Ñï} [inst : DecidableEq (Fin n)]
    {c : Fin n ‚Üí S.C} (p : Pure S c) (i : Fin n)
    (x y : S.FD.obj (Discrete.mk (c i))) (b : ComponentIdx c) :
    (update p i (x + y)).component b = (update p i x).component b +
    (update p i y).component b := by
  cases n
  ¬∑ exact Fin.elim0 i
  rename_i n
  rw [component_eq_drop _ i, component_eq_drop _ i, component_eq_drop _ i]
  simp [add_mul]

@[simp]
lemma component_update_smul {n : ‚Ñï} [inst : DecidableEq (Fin n)]
    {c : Fin n ‚Üí S.C} (p : Pure S c) (i : Fin n)
    (x : k) (y : S.FD.obj (Discrete.mk (c i))) (b : ComponentIdx c) :
    (update p i (x ‚Ä¢ y)).component b = x * (update p i y).component b := by
  cases n
  ¬∑ exact Fin.elim0 i
  rename_i n
  rw [component_eq_drop _ i, component_eq_drop _ i]
  simp only [update_same, map_smul, Finsupp.coe_smul, Pi.smul_apply, smul_eq_mul, update_drop_self]
  ring

/-- The multilinear map taking pure tensors `p` to a map `ComponentIdx c ‚Üí k` which when
  evaluated returns the components of `p`. -/
noncomputable def componentMap {n : ‚Ñï} (c : Fin n ‚Üí S.C) :
    MultilinearMap k (fun i => S.FD.obj (Discrete.mk (c i))) (ComponentIdx c ‚Üí k) where
  toFun p := fun b => component p b
  map_update_add' p i x y := by
    ext b
    change component (update p i (x + y)) b =
      component (update p i x) b + component (update p i y) b
    exact component_update_add p i x y b
  map_update_smul' p i x y := by
    ext b
    change component (update p i (x ‚Ä¢ y)) b = x * component (update p i y) b
    exact component_update_smul p i x y b

@[simp]
lemma componentMap_apply {n : ‚Ñï} (c : Fin n ‚Üí S.C)
    (p : Pure S c) : componentMap c p = p.component := by
  rfl

/-- Given an component idx `b` in `ComponentIdx c`, `basisVector c b` is the pure tensor
  formed by `S.basis (c i) (b i)`. -/
noncomputable def basisVector {n : ‚Ñï} (c : Fin n ‚Üí S.C) (b : ComponentIdx c) : Pure S c :=
  fun i => S.basis (c i) (b i)

@[simp]
lemma component_basisVector {n : ‚Ñï} (c : Fin n ‚Üí S.C) (b1 b2 : ComponentIdx c) :
    (basisVector c b1).component b2 = if b1 = b2 then 1 else 0 := by
  simp only [basisVector, component_eq, funext_iff]
  simp only [component, MultilinearMap.coe_mk,
    Basis.repr_self]
  by_cases h : b1 = b2
  ¬∑ subst h
    simp
  ¬∑ rw [funext_iff] at h
    simp only [not_forall] at h
    obtain ‚ü®i, hi‚ü© := h
    split
    next h => simp_all only [not_true_eq_false]
    next h =>
      simp_all only [not_forall]
      obtain ‚ü®w, h‚ü© := h
      refine Finset.prod_eq_zero (Finset.mem_univ i) ?_
      rw [Finsupp.single_eq_of_ne hi]

end Pure

lemma induction_on_pure {n : ‚Ñï} {c : Fin n ‚Üí S.C} {P : S.Tensor c ‚Üí Prop}
    (h : ‚àÄ (p : Pure S c), P p.toTensor)
    (hsmul : ‚àÄ (r : k) t, P t ‚Üí P (r ‚Ä¢ t))
    (hadd : ‚àÄ t1 t2, P t1 ‚Üí P t2 ‚Üí P (t1 + t2)) (t : S.Tensor c) : P t := by
  refine PiTensorProduct.induction_on' t ?_ ?_
  ¬∑ intro r p
    simpa using hsmul r _ (h p)
  ¬∑ intro t1 t2
    exact fun a a_1 => hadd t1 t2 a a_1

/-!

## The basis

-/

noncomputable section Basis

/-- The linear map from tensors to its components. -/
def componentMap {n : ‚Ñï} (c : Fin n ‚Üí S.C) : S.Tensor c ‚Üí‚Çó[k] (ComponentIdx c ‚Üí k) :=
  PiTensorProduct.lift (Pure.componentMap c)

@[simp]
lemma componentMap_pure {n : ‚Ñï} (c : Fin n ‚Üí S.C)
    (p : Pure S c) : componentMap c (p.toTensor) = Pure.componentMap c p := by
  simp only [componentMap, Pure.toTensor]
  change (PiTensorProduct.lift (Pure.componentMap c)) ((PiTensorProduct.tprod k) p) = _
  simp [PiTensorProduct.lift_tprod]

/-- The tensor created from it's components. -/
def ofComponents {n : ‚Ñï} (c : Fin n ‚Üí S.C) :
    (ComponentIdx c ‚Üí k) ‚Üí‚Çó[k] S.Tensor c where
  toFun f := ‚àë b, f b ‚Ä¢ (Pure.basisVector c b).toTensor
  map_add' fb gb := by
    simp [add_smul, Finset.sum_add_distrib]
  map_smul' fb r := by
    simp [smul_smul, Finset.smul_sum]

@[simp]
lemma componentMap_ofComponents {n : ‚Ñï} (c : Fin n ‚Üí S.C) (f : ComponentIdx c ‚Üí k) :
    componentMap c (ofComponents c f) = f := by
  ext b
  simp [ofComponents]

@[simp]
lemma ofComponents_componentMap {n : ‚Ñï} (c : Fin n ‚Üí S.C) (t : S.Tensor c) :
    ofComponents c (componentMap c t) = t := by
  simp only [ofComponents, LinearMap.coe_mk, AddHom.coe_mk]
  apply induction_on_pure ?_ ?_ ?_ t
  ¬∑ intro p
    simp only [componentMap_pure, Pure.componentMap_apply]
    have h1 (x : ComponentIdx c) : p.component x ‚Ä¢ (Pure.basisVector c x).toTensor =
        Pure.toTensor (fun i => ((S.basis (c i)).repr (p i)) (x i) ‚Ä¢ (S.basis (c i)) (x i)) := by
      rw [Pure.component_eq, Pure.toTensor]
      exact Eq.symm (MultilinearMap.map_smul_univ (PiTensorProduct.tprod k)
          (fun i => ((S.basis (c i)).repr (p i)) (x i)) fun i => (S.basis (c i)) (x i))
    conv_lhs =>
      enter [2, x]
      rw [h1]
    trans (PiTensorProduct.tprod k) fun i =>
      ‚àë x, ((S.basis (c i)).repr (p i)) x ‚Ä¢ (S.basis (c i)) x
    ¬∑ exact (MultilinearMap.map_sum (PiTensorProduct.tprod k) fun i j =>
        ((S.basis (c i)).repr (p i)) j ‚Ä¢ (S.basis (c i)) j).symm
    congr
    funext i
    exact Basis.sum_equivFun (S.basis (c i)) (p i)
  ¬∑ intro r t ht
    simp only [map_smul, Pi.smul_apply, smul_eq_mul, ‚Üê smul_smul]
    conv_rhs => rw [‚Üê ht]
    exact Eq.symm Finset.smul_sum
  ¬∑ intro t1 t2 h1 h2
    simp [add_smul, Finset.sum_add_distrib, h1, h2]

/-- The basis of tensors. -/
def basis {n : ‚Ñï} (c : Fin n ‚Üí S.C) : Basis (ComponentIdx c) k (S.Tensor c) where
  repr := (LinearEquiv.mk (componentMap c) (ofComponents c)
    (fun x => by simp) (fun x => by simp)).trans
    (Finsupp.linearEquivFunOnFinite k k ((j : Fin n) ‚Üí Fin (S.repDim (c j)))).symm

lemma basis_apply {n : ‚Ñï} (c : Fin n ‚Üí S.C) (b : ComponentIdx c) :
    basis c b = (Pure.basisVector c b).toTensor := by
  change ofComponents c _ = _
  simp only [ofComponents, LinearEquiv.coe_toEquiv_symm, LinearEquiv.symm_symm, EquivLike.coe_coe,
    Finsupp.linearEquivFunOnFinite_single, LinearMap.coe_mk, AddHom.coe_mk]
  rw [Finset.sum_eq_single b]
  ¬∑ simp
  ¬∑ intro b' _ hb
    rw [Pi.single_apply]
    simp [hb]
  ¬∑ simp

lemma induction_on_basis {n : ‚Ñï} {c : Fin n ‚Üí S.C} {P : S.Tensor c ‚Üí Prop}
    (h : ‚àÄ b, P (basis c b)) (hzero : P 0)
    (hsmul : ‚àÄ (r : k) t, P t ‚Üí P (r ‚Ä¢ t))
    (hadd : ‚àÄ t1 t2, P t1 ‚Üí P t2 ‚Üí P (t1 + t2)) (t : S.Tensor c) : P t := by
  let Pt (t : S.Tensor c)
      (ht : t ‚àà Submodule.span k (Set.range (basis c))) := P t
  change Pt t (Basis.mem_span _ t)
  apply Submodule.span_induction
  ¬∑ intro x hx
    obtain ‚ü®b, rfl‚ü© := Set.mem_range.mp hx
    exact h b
  ¬∑ simp [Pt, hzero]
  ¬∑ intro t1 t2 h1 h2
    exact fun a a_1 => hadd t1 t2 a a_1
  ¬∑ intro r t ht
    exact fun a => hsmul r t a

end Basis
/-!

## The action
-/

namespace Pure

noncomputable instance actionP : MulAction S.G (Pure S c) where
  smul g p := fun i => (S.FD.obj _).œÅ g (p i)
  one_smul p := by
    ext i
    change (S.FD.obj _).œÅ 1 (p i) = p i
    simp
  mul_smul g g' p := by
    ext i
    change (S.FD.obj _).œÅ (g * g') (p i) = (S.FD.obj _).œÅ g ((S.FD.obj _).œÅ g' (p i))
    simp

noncomputable instance : SMul (S.G) (Pure S c) := actionP.toSMul

lemma actionP_eq {g : S.G} {p : Pure S c} : g ‚Ä¢ p = fun i => (S.FD.obj _).œÅ g (p i) := rfl

@[simp]
lemma drop_actionP {n : ‚Ñï} {c : Fin (n + 1) ‚Üí S.C} {i : Fin (n + 1)} {p : Pure S c} (g : S.G) :
    (g ‚Ä¢ p).drop i = g ‚Ä¢ (p.drop i) := by
  ext j
  rw [drop, actionP_eq, actionP_eq]
  simp only [Function.comp_apply]
  rfl

end Pure

noncomputable instance actionT : MulAction S.G (S.Tensor c) where
  smul g t := (S.F.obj (OverColor.mk c)).œÅ g t
  one_smul t := by
    change (S.F.obj (OverColor.mk c)).œÅ 1 t = t
    simp
  mul_smul g g' t := by
    change (S.F.obj (OverColor.mk c)).œÅ (g * g') t =
      (S.F.obj (OverColor.mk c)).œÅ g ((S.F.obj (OverColor.mk c)).œÅ g' t)
    simp

lemma actionT_eq {g : S.G} {t : S.Tensor c} : g ‚Ä¢ t = (S.F.obj (OverColor.mk c)).œÅ g t := rfl

lemma actionT_pure {g : S.G} {p : Pure S c} :
    g ‚Ä¢ p.toTensor = Pure.toTensor (g ‚Ä¢ p) := by
  rw [actionT_eq, Pure.toTensor]
  simp only [F_def, lift, lift.obj', LaxBraidedFunctor.of_toFunctor]
  rw [OverColor.lift.objObj'_œÅ_tprod]
  rfl

@[simp]
lemma actionT_add {g : S.G} {t1 t2 : S.Tensor c} :
    g ‚Ä¢ (t1 + t2) = g ‚Ä¢ t1 + g ‚Ä¢ t2 := by
  rw [actionT_eq, actionT_eq, actionT_eq]
  simp

@[simp]
lemma actionT_smul {g : S.G} {r : k} {t : S.Tensor c} :
    g ‚Ä¢ (r ‚Ä¢ t) = r ‚Ä¢ (g ‚Ä¢ t) := by
  rw [actionT_eq, actionT_eq]
  simp

@[simp]
lemma actionT_zero {g : S.G} : g ‚Ä¢ (0 : S.Tensor c) = 0 := by
  simp [actionT_eq]

/-!

## Permutations

And their interactions with
- actions
-/

/-- Given two lists of indices `c : Fin n ‚Üí S.C` and `c1 : Fin m ‚Üí S.C` a map
  `œÉ : Fin m ‚Üí Fin n` satisfies the condition `PermCond c c1 œÉ` if it is:
- A bijection
- Forms a commutative triangle with `c` and `c1`.
-/
def PermCond {n m : ‚Ñï} (c : Fin n ‚Üí S.C) (c1 : Fin m ‚Üí S.C)
    (œÉ : Fin m ‚Üí Fin n) : Prop :=
  Function.Bijective œÉ ‚àß ‚àÄ i, c (œÉ i) = c1 i

/-- For a map `œÉ` satisfying `PermCond c c1 œÉ`, the inverse of that map. -/
def PermCond.inv {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    (œÉ : Fin m ‚Üí Fin n) (h : PermCond c c1 œÉ) : Fin n ‚Üí Fin m :=
  Fintype.bijInv h.1

/-- For a map `œÉ : Fin m ‚Üí Fin n` satisfying `PermCond c c1 œÉ`,
  that map lifted to an equivalence between
  `Fin n` and `Fin m`. -/
def PermCond.toEquiv {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    {œÉ : Fin m ‚Üí Fin n} (h : PermCond c c1 œÉ) :
    Fin n ‚âÉ Fin m where
  toFun := PermCond.inv œÉ h
  invFun := œÉ
  left_inv := Fintype.rightInverse_bijInv h.1
  right_inv := Fintype.leftInverse_bijInv h.1

@[simp]
lemma PermCond.toHom_id {n : ‚Ñï} {c c1 : Fin n ‚Üí S.C} (h : PermCond c c1 id) :
    PermCond.toEquiv h = Equiv.refl _ := by
  apply Equiv.symm_bijective.injective
  simp [PermCond.toEquiv]
  ext a
  simp

lemma PermCond.preserve_color {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    {œÉ : Fin m ‚Üí Fin n} (h : PermCond c c1 œÉ) :
    ‚àÄ (x : Fin m), c1 x = (c ‚àò œÉ) x := by
  intro x
  obtain ‚ü®y, rfl‚ü© := h.toEquiv.surjective x
  simp only [Function.comp_apply, Equiv.symm_apply_apply]
  rw [h.2]

/-- For a map `œÉ : Fin m ‚Üí Fin n` satisfying `PermCond c c1 œÉ`,
  that map lifted to a morphism in the `OverColor C` category. -/
def PermCond.toHom {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    {œÉ : Fin m ‚Üí Fin n} (h : PermCond c c1 œÉ) :
    OverColor.mk c ‚ü∂ OverColor.mk c1 :=
  equivToHomEq (h.toEquiv) (h.preserve_color)

@[simp]
lemma PermCond.toHom_id_refl {n : ‚Ñï} {c : Fin n ‚Üí S.C} :
    PermCond.toHom (by simp [PermCond] : PermCond c c id) = ùüô (OverColor.mk c) := by
  simp [PermCond.toHom]
  rfl

/-- Given a morphism in the `OverColor C` between `c` and `c1` category the corresponding morphism
  `(Hom.toEquiv œÉ).symm` satisfies the `PermCond`. -/
lemma PermCond.ofHom {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    (œÉ : OverColor.mk c ‚ü∂ OverColor.mk c1) :
    PermCond c c1 (Hom.toEquiv œÉ).symm := by
  apply And.intro
  ¬∑ exact Equiv.bijective (Hom.toEquiv œÉ).symm
  ¬∑ intro x
    simpa [OverColor.mk_hom] using Hom.toEquiv_symm_apply œÉ x

/-- The composition of two maps satisfying `PermCond` also satifies the `PermCond`. -/
lemma PermCond.comp {n n1 n2 : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin n1 ‚Üí S.C}
    {c2 : Fin n2 ‚Üí S.C} {œÉ : Fin n1 ‚Üí Fin n} {œÉ2 : Fin n2 ‚Üí Fin n1}
    (h : PermCond c c1 œÉ) (h2 : PermCond c1 c2 œÉ2) :
    PermCond c c2 (œÉ ‚àò œÉ2) := by
  apply And.intro
  ¬∑ refine Function.Bijective.comp h.1 h2.1
  ¬∑ intro x
    simp only [Function.comp_apply]
    rw [h.2, h2.2]

TODO "Prove that if `œÉ` satifies `PermCond c c1 œÉ` then `PermCond.inv œÉ h`
  satifies `PermCond c1 c (PermCond.inv œÉ h)`."

lemma fin_cast_permCond (n n1 : ‚Ñï) {c : Fin n ‚Üí S.C} (h : n1 = n) :
    PermCond c (c ‚àò Fin.cast h) (Fin.cast h) := by
  apply And.intro
  ¬∑ exact Equiv.bijective (finCongr h)
  ¬∑ intro i
    rfl
/-!

## Permutations

-/

/-- Given a permutation `œÉ : Fin m ‚Üí Fin n` of indices satisfying `PermCond` through `h`,
  and a pure tensor `p`, `permP œÉ h p` is the pure tensor permuted accordinge to `œÉ`.

  For example if `m = n = 2` and `œÉ = ![1, 0]`, and `p = v ‚äó‚Çú w` then
  `permP œÉ _ p = w ‚äó‚Çú v`. -/
def Pure.permP {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    (œÉ : Fin m ‚Üí Fin n) (h : PermCond c c1 œÉ) (p : Pure S c) : Pure S c1 :=
  fun i => S.FD.map (eqToHom (by simp [h.preserve_color])) (p (œÉ i))

@[simp]
lemma Pure.permP_basisVector {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    (œÉ : Fin m ‚Üí Fin n) (h : PermCond c c1 œÉ) (b : ComponentIdx c) :
    Pure.permP œÉ h (Pure.basisVector c b) =
    Pure.basisVector c1 (fun i => Fin.cast (by simp [h.preserve_color]) (b (œÉ i))) := by
  ext i
  simp only [permP, basisVector]
  have h1 {c1 c2 : S.C} (h : c1 = c2) (x : Fin (S.repDim c1)) :
      S.FD.map (eqToHom (by simp [h])) ((S.basis (c1)) x) =
      (S.basis c2) (Fin.cast (by simp [h]) x) := by
    subst h
    simp
  apply h1
  simp [h.preserve_color]

lemma PermCond.by_decide {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    {œÉ : Fin m ‚Üí Fin n} (h : PermCond c c1 œÉ := by decide) :
    PermCond c c1 œÉ  := h

/-- Given a permutation `œÉ : Fin m ‚Üí Fin n` of indices satisfying `PermCond` through `h`,
  and a tensor `t`, `permT œÉ h t` is the tensor tensor permuted accordinge to `œÉ`. -/
noncomputable def permT {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    (œÉ : Fin m ‚Üí Fin n) (h : PermCond c c1 œÉ) : S.Tensor c ‚Üí‚Çó[k] S.Tensor c1 where
  toFun t := (ConcreteCategory.hom (S.F.map h.toHom).hom) t
  map_add' t1 t2 := by
    simp
  map_smul' r t := by
    simp

@[simp]
lemma permT_id_rfl {n : ‚Ñï} {c : Fin n ‚Üí S.C} :
    permT id (by simp [PermCond] : PermCond c c id) = LinearMap.id := by
  ext t
  simp [permT]

lemma permT_pure {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    {œÉ : Fin m ‚Üí Fin n} (h : PermCond c c1 œÉ) (p : Pure S c) :
    permT œÉ h p.toTensor = (p.permP œÉ h).toTensor := by
  simp only [F_def, permT, Pure.toTensor, LinearMap.coe_mk, AddHom.coe_mk]
  rw [OverColor.lift.map_tprod]
  rfl

@[simp]
lemma permT_equivariant {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    {œÉ : Fin m ‚Üí Fin n} (h : PermCond c c1 œÉ) (g : S.G) (t : S.Tensor c) :
    permT œÉ h (g ‚Ä¢ t) = g ‚Ä¢ permT œÉ h t := by
  simp only [permT, actionT_eq, LinearMap.coe_mk, AddHom.coe_mk]
  exact Rep.hom_comm_apply (S.F.map h.toHom) g t

@[congr]
lemma Pure.permP_congr {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    {œÉ œÉ1 : Fin m ‚Üí Fin n} {h : PermCond c c1 œÉ} {h1 : PermCond c c1 œÉ1}
    {p p1 : Pure S c} (hmap : œÉ = œÉ1) (hpure : p = p1) :
    Pure.permP œÉ h p = Pure.permP œÉ1 h1 p1 := by
  subst hmap hpure
  rfl

@[congr]
lemma permT_congr {n m : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m ‚Üí S.C}
    {œÉ œÉ1 : Fin m ‚Üí Fin n} {h : PermCond c c1 œÉ} {h1 : PermCond c c1 œÉ1}
    (hmap : œÉ = œÉ1) {t t1: S.Tensor c} (htensor : t = t1) :
    permT œÉ h t = permT œÉ1 h1 t1 := by
  subst hmap htensor
  rfl

@[simp]
lemma Pure.permP_permP {n m1 m2 : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m1 ‚Üí S.C} {c2 : Fin m2 ‚Üí S.C}
    {œÉ : Fin m1 ‚Üí Fin n} {œÉ2 : Fin m2 ‚Üí Fin m1} (h : PermCond c c1 œÉ) (h2 : PermCond c1 c2 œÉ2)
    (p : Pure S c) :
    Pure.permP œÉ2 h2 (Pure.permP œÉ h p) = Pure.permP (œÉ ‚àò œÉ2) (PermCond.comp h h2) p := by
  ext i
  simp [permP, Pure.permP, Function.comp_apply, map_map_apply]

@[simp]
lemma permT_permT {n m1 m2 : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin m1 ‚Üí S.C} {c2 : Fin m2 ‚Üí S.C}
    {œÉ : Fin m1 ‚Üí Fin n} {œÉ2 : Fin m2 ‚Üí Fin m1} (h : PermCond c c1 œÉ) (h2 : PermCond c1 c2 œÉ2)
    (t : S.Tensor c) :
    permT œÉ2 h2 (permT œÉ h t) = permT (œÉ ‚àò œÉ2) (PermCond.comp h h2) t := by
  let P (t : S.Tensor c) := permT œÉ2 h2 (permT œÉ h t) = permT (œÉ ‚àò œÉ2) (PermCond.comp h h2) t
  change P t
  apply induction_on_basis
  ¬∑ intro b
    simp only [P]
    rw [basis_apply, permT_pure, permT_pure, permT_pure]
    simp
  ¬∑ simp [P]
  ¬∑ intro r t h1
    simp_all [P]
  ¬∑ intro t1 t2 h1 h2
    simp_all [P]
/-!

## Product

And its interaction with
- actions
- permutations

-/

TODO "Change products of tensors to use `Fin.append` rather then
  `Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm`."

/-- The equivalence between `ComponentIdx (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm)` and
  `Œ† (i : Fin n1 ‚äï Fin n2), Fin (S.repDim (Sum.elim c c1 i))`. -/
def ComponentIdx.prodEquiv {n1 n2 : ‚Ñï} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C} :
    ComponentIdx (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm) ‚âÉ
    Œ† (i : Fin n1 ‚äï Fin n2), Fin (S.repDim (Sum.elim c c1 i)) :=
  (Equiv.piCongr finSumFinEquiv (fun _ => finCongr (by simp))).symm

/-- The product of two component indices. -/
def ComponentIdx.prod {n1 n2 : ‚Ñï} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (b : ComponentIdx c) (b1 : ComponentIdx c1) :
    ComponentIdx (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm) :=
  ComponentIdx.prodEquiv.symm fun | Sum.inl i => b i | Sum.inr i => b1 i

lemma ComponentIdx.prod_apply_finSumFinEquiv {n1 n2 : ‚Ñï} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (b : ComponentIdx c) (b1 : ComponentIdx c1) (i : Fin n1 ‚äï Fin n2) :
    ComponentIdx.prod b b1 (finSumFinEquiv i) =
    match i with
    | Sum.inl i => Fin.cast (by simp) (b i)
    | Sum.inr i => Fin.cast (by simp) (b1 i) := by
  rw [ComponentIdx.prod]
  rw [prodEquiv]
  simp only [Equiv.symm_symm]
  rw [Equiv.piCongr_apply_apply]
  match i with
  | Sum.inl i =>
    rfl
  | Sum.inr i =>
    rfl

/-- The equivalence between pure tensors based on a product of lists of indices, and
  the type `Œ† (i : Fin n1 ‚äï Fin n2), S.FD.obj (Discrete.mk ((Sum.elim c c1) i))`. -/
def Pure.prodEquiv {n1 n2 : ‚Ñï} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C} :
    Pure S (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm) ‚âÉ
    Œ† (i : Fin n1 ‚äï Fin n2), S.FD.obj (Discrete.mk ((Sum.elim c c1) i)) :=
  (Equiv.piCongr finSumFinEquiv
  (fun _ => ((Action.forget _ _).mapIso
    (S.FD.mapIso (Discrete.eqToIso (by simp)))).toLinearEquiv.toEquiv)).symm

/-- Given two pure tensors `p1 : Pure S c` and `p2 : Pure S c`, `prodP p p2` is the tensor
  product of those tensors returning an element in
  `Pure S (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm)`. -/
def Pure.prodP {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (p1 : Pure S c) (p2 : Pure S c1) : Pure S (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm) :=
  Pure.prodEquiv.symm fun | Sum.inl i => p1 i | Sum.inr i => p2 i

lemma Pure.prodP_apply_finSumFinEquiv {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (p1 : Pure S c) (p2 : Pure S c1) (i : Fin n1 ‚äï Fin n2) :
    Pure.prodP p1 p2 (finSumFinEquiv i) =
    match i with
    | Sum.inl i => S.FD.map (eqToHom (by simp)) (p1 i)
    | Sum.inr i => S.FD.map (eqToHom (by simp)) (p2 i) := by
  rw [Pure.prodP]
  rw [prodEquiv]
  simp only [Equiv.symm_symm]
  rw [Equiv.piCongr_apply_apply]
  match i with
  | Sum.inl i =>
    rfl
  | Sum.inr i =>
    rfl

@[simp]
lemma Pure.prodP_apply_castAdd {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (p1 : Pure S c) (p2 : Pure S c1) (i : Fin n1) :
    Pure.prodP p1 p2 (Fin.castAdd n2 i) =
    S.FD.map (eqToHom (by simp)) (p1 i) := by
  trans Pure.prodP p1 p2 (finSumFinEquiv (Sum.inl i))
  ¬∑ rfl
  rw [Pure.prodP_apply_finSumFinEquiv]
  simp

@[simp]
lemma Pure.prodP_apply_natAdd {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (p1 : Pure S c) (p2 : Pure S c1) (i : Fin n2) :
    Pure.prodP p1 p2 (Fin.natAdd n1 i) =
    S.FD.map (eqToHom (by simp)) (p2 i) := by
  trans Pure.prodP p1 p2 (finSumFinEquiv (Sum.inr i))
  ¬∑ rfl
  rw [Pure.prodP_apply_finSumFinEquiv]
  simp

lemma Pure.prodP_basisVector {n n1 : ‚Ñï} {c : Fin n ‚Üí S.C} {c1 : Fin n1 ‚Üí S.C}
    (b : ComponentIdx c) (b1 : ComponentIdx c1) :
    Pure.prodP (Pure.basisVector c b) (Pure.basisVector c1 b1) =
    Pure.basisVector _ (b.prod b1) := by
  ext i
  obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
  rw [Pure.prodP_apply_finSumFinEquiv]
  have basis_congr {c1 c2 : S.C} (h : c1 = c2) (x : Fin (S.repDim c1))
    (y : Fin (S.repDim c2)) (hxy : y = Fin.cast (by simp [h]) x) :
      S.FD.map (eqToHom (by simp [h])) ((S.basis c1) x) =
      (S.basis c2) y := by
    subst h hxy
    simp
  match i with
  | Sum.inl i =>
    simp only [Function.comp_apply, basisVector]
    apply basis_congr
    ¬∑ rw [ComponentIdx.prod_apply_finSumFinEquiv]
    ¬∑ simp
  | Sum.inr i =>
    simp only [Function.comp_apply, basisVector]
    apply basis_congr
    ¬∑ rw [ComponentIdx.prod_apply_finSumFinEquiv]
    ¬∑ simp

/-- The equivalence between the type `S.F.obj (OverColor.mk (Sum.elim c c1))` and the type
  `S.Tensor (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm)`. -/
noncomputable def prodEquiv {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C} :
    S.F.obj (OverColor.mk (Sum.elim c c1)) ‚âÉ‚Çó[k] S.Tensor (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm) :=
  ((Action.forget _ _).mapIso (S.F.mapIso (OverColor.equivToIso finSumFinEquiv))).toLinearEquiv

TODO "Determine in `prodEquiv_symm_pure` why in `rw (transparency := .instances) [h1]`
  `(transparency := .instances)` is needed. As a first step adding this as a comment here."

lemma prodEquiv_symm_pure {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (p : Pure S (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm)) :
    prodEquiv.symm p.toTensor = PiTensorProduct.tprod k (Pure.prodEquiv p) := by
  rw [prodEquiv]
  change (S.F.map (OverColor.equivToIso finSumFinEquiv).inv).hom p.toTensor = _
  rw [Pure.toTensor]
  have h1 := OverColor.lift.map_tprod S.FD (equivToIso finSumFinEquiv).inv p
  simp only [F_def]
  rw (transparency := .instances) [h1]
  rfl

/-- The tensor product of two tensors as a bi-linear map from
  `S.Tensor c` and `S.Tensor c1` to `S.Tensor (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm)`. -/
noncomputable def prodT {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C} :
    S.Tensor c ‚Üí‚Çó[k] S.Tensor c1 ‚Üí‚Çó[k] S.Tensor (Sum.elim c c1 ‚àò ‚áëfinSumFinEquiv.symm) := by
  refine LinearMap.mk‚ÇÇ k ?_ ?_ ?_ ?_ ?_
  ¬∑ exact fun t1 t2 => prodEquiv ((Functor.LaxMonoidal.Œº S.F _ _).hom (t1 ‚äó‚Çú t2))
  ¬∑ intro t1 t2 t3
    simp [TensorProduct.add_tmul]
  ¬∑ intro n t1 t2
    simp [TensorProduct.smul_tmul]
  ¬∑ intro t1 t2 t3
    simp [TensorProduct.tmul_add]
  ¬∑ intro n t1 t2
    simp [TensorProduct.tmul_smul]

lemma prodT_pure {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (t : Pure S c) (t1 : Pure S c1) :
    (t.toTensor).prodT (t1.toTensor) = (Pure.prodP t t1).toTensor := by
  simp only [prodT, LinearMap.mk‚ÇÇ_apply]
  conv_lhs =>
    enter [2]
    rw [Pure.Œº_toTensor_tmul_toTensor]
  change prodEquiv.toEquiv _ = _
  rw [Equiv.apply_eq_iff_eq_symm_apply]
  simp only [instMonoidalCategoryStruct_tensorObj_left, instMonoidalCategoryStruct_tensorObj_hom,
    Functor.id_obj, LinearEquiv.coe_toEquiv_symm, EquivLike.coe_coe]
  rw [prodEquiv_symm_pure]
  congr
  simp only [Pure.prodP, Equiv.apply_symm_apply]
  ext i
  match i with
  | Sum.inl i =>
    rfl
  | Sum.inr i =>
    rfl

/-

## Product Maps

These maps are used in permutations of tensors.
-/

/-- Given a map `œÉ : Fin n ‚Üí Fin n'`, the induced map `Fin (n + n2) ‚Üí Fin (n' + n2)`. -/
def prodLeftMap (n2 : ‚Ñï) (œÉ : Fin n ‚Üí Fin n') : Fin (n + n2) ‚Üí Fin (n' + n2) :=
    finSumFinEquiv ‚àò Sum.map œÉ id ‚àò finSumFinEquiv.symm

/-- Given a map `œÉ : Fin n ‚Üí Fin n'`, the induced map `Fin (n2 + n) ‚Üí Fin (n2 + n')`. -/
def prodRightMap (n2 : ‚Ñï) (œÉ : Fin n ‚Üí Fin n') : Fin (n2 + n) ‚Üí Fin (n2 + n') :=
    finSumFinEquiv ‚àò Sum.map id œÉ ‚àò finSumFinEquiv.symm

/-- The map between `Fin (n1 + n2 + n3)` and `Fin (n1 + (n2 + n3))` formed by casting. -/
def prodAssocMap (n1 n2 n3 : ‚Ñï) : Fin (n1 + n2 + n3) ‚Üí Fin (n1 + (n2 + n3)) :=
    Fin.cast (Nat.add_assoc n1 n2 n3)

@[simp]
lemma prodAssocMap_castAdd_castAdd {n1 n2 n3 : ‚Ñï} (i : Fin n1) :
    prodAssocMap n1 n2 n3 (Fin.castAdd n3 (Fin.castAdd n2 i)) =
    finSumFinEquiv (Sum.inl i) := by
  simp [prodAssocMap, Fin.castAdd, Fin.ext_iff]

@[simp]
lemma prodAssocMap_castAdd_natAdd {n1 n2 n3 : ‚Ñï} (i : Fin n2) :
    prodAssocMap n1 n2 n3 (Fin.castAdd n3 (Fin.natAdd n1 i)) =
    finSumFinEquiv (Sum.inr (finSumFinEquiv (Sum.inl i))) := by
  simp [prodAssocMap, Fin.castAdd, Fin.ext_iff]

@[simp]
lemma prodAssocMap_natAdd {n1 n2 n3 : ‚Ñï} (i : Fin (n3)) :
    prodAssocMap n1 n2 n3 (Fin.natAdd (n1 + n2) i) =
    finSumFinEquiv (Sum.inr (finSumFinEquiv (Sum.inr i))) := by
  simp only [prodAssocMap, finSumFinEquiv_apply_right, Fin.ext_iff, Fin.coe_cast, Fin.coe_natAdd]
  omega

/-- The map between `Fin (n1 + (n2 + n3))` and `Fin (n1 + n2 + n3)` formed by casting. -/
def prodAssocMap' (n1 n2 n3 : ‚Ñï) : Fin (n1 + (n2 + n3)) ‚Üí Fin (n1 + n2 + n3) :=
    Fin.cast (Nat.add_assoc n1 n2 n3).symm

@[simp]
lemma prodAssocMap'_castAdd {n1 n2 n3 : ‚Ñï} (i : Fin n1) :
    prodAssocMap' n1 n2 n3 (Fin.castAdd (n2 + n3) i) =
    finSumFinEquiv (Sum.inl (finSumFinEquiv (Sum.inl i))) := by
  simp [prodAssocMap', Fin.castAdd, Fin.ext_iff]

@[simp]
lemma prodAssocMap'_natAdd_castAdd {n1 n2 n3 : ‚Ñï} (i : Fin n2) :
    prodAssocMap' n1 n2 n3 (Fin.natAdd n1 (Fin.castAdd n3 i)) =
    finSumFinEquiv (Sum.inl (finSumFinEquiv (Sum.inr i))) := by
  simp [prodAssocMap', Fin.castAdd, Fin.ext_iff]

@[simp]
lemma prodAssocMap'_natAdd_natAdd {n1 n2 n3 : ‚Ñï} (i : Fin n3) :
    prodAssocMap' n1 n2 n3 (Fin.natAdd n1 (Fin.natAdd n2 i)) =
    finSumFinEquiv (Sum.inr i) := by
  simp only [prodAssocMap', finSumFinEquiv_apply_right, Fin.ext_iff, Fin.coe_cast, Fin.coe_natAdd]
  omega

/-- The map between `Fin (n1 + n2)` and `Fin (n2 + n1)` formed by swapping elements. -/
def prodSwapMap (n1 n2 : ‚Ñï) : Fin (n1 + n2) ‚Üí Fin (n2 + n1) :=
    finSumFinEquiv ‚àò Sum.swap ‚àò finSumFinEquiv.symm

@[simp]
lemma prodLeftMap_permCond {œÉ : Fin n' ‚Üí Fin n} (c2 : Fin n2 ‚Üí S.C) (h : PermCond c c' œÉ) :
    PermCond (Sum.elim c c2 ‚àò finSumFinEquiv.symm)
      (Sum.elim c' c2 ‚àò finSumFinEquiv.symm) (prodLeftMap n2 œÉ) := by
  apply And.intro
  ¬∑ rw [prodLeftMap]
    refine (Equiv.comp_bijective (Sum.map œÉ id ‚àò ‚áëfinSumFinEquiv.symm) finSumFinEquiv).mpr ?_
    refine (Equiv.bijective_comp finSumFinEquiv.symm (Sum.map œÉ id)).mpr ?_
    refine Sum.map_bijective.mpr ?_
    apply And.intro
    ¬∑ exact h.1
    ¬∑ exact Function.bijective_id
  ¬∑ intro i
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    simp only [prodLeftMap, Function.comp_apply, Equiv.symm_apply_apply]
    match i with
    | Sum.inl i => simp [h.2]
    | Sum.inr i => rfl

@[simp]
lemma prodRightMap_permCond {œÉ : Fin n' ‚Üí Fin n} (c2 : Fin n2 ‚Üí S.C) (h : PermCond c c' œÉ) :
    PermCond (Sum.elim c2 c ‚àò finSumFinEquiv.symm)
      (Sum.elim c2 c' ‚àò finSumFinEquiv.symm) (prodRightMap n2 œÉ) := by
  apply And.intro
  ¬∑ rw [prodRightMap]
    refine (Equiv.comp_bijective (Sum.map id œÉ ‚àò ‚áëfinSumFinEquiv.symm) finSumFinEquiv).mpr ?_
    refine (Equiv.bijective_comp finSumFinEquiv.symm (Sum.map id œÉ)).mpr ?_
    refine Sum.map_bijective.mpr ?_
    apply And.intro
    ¬∑ exact Function.bijective_id
    ¬∑ exact h.1
  ¬∑ intro i
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    simp only [prodRightMap, Function.comp_apply, Equiv.symm_apply_apply]
    match i with
    | Sum.inl i => rfl
    | Sum.inr i => simp [h.2]

@[simp]
lemma prodSwapMap_permCond {n1 n2 : ‚Ñï} {c : Fin n1 ‚Üí S.C} {c2 : Fin n2 ‚Üí S.C} :
    PermCond (Sum.elim c c2 ‚àò finSumFinEquiv.symm)
      (Sum.elim c2 c ‚àò finSumFinEquiv.symm) (prodSwapMap n2 n1) := by
  apply And.intro
  ¬∑ dsimp only [prodSwapMap]
    refine (Equiv.comp_bijective (Sum.swap ‚àò ‚áëfinSumFinEquiv.symm) finSumFinEquiv).mpr ?_
    refine (Equiv.bijective_comp finSumFinEquiv.symm Sum.swap).mpr ?_
    exact Function.bijective_iff_has_inverse.mpr ‚ü®Sum.swap, by simp‚ü©
  ¬∑ intro i
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    simp only [prodSwapMap, Function.comp_apply, Equiv.symm_apply_apply]
    match i with
    | Sum.inl i => rfl
    | Sum.inr i => rfl

@[simp]
lemma prodAssocMap_permCond {n1 n2 n3 : ‚Ñï} {c : Fin n1 ‚Üí S.C} {c2 : Fin n2 ‚Üí S.C}
    {c3 : Fin n3 ‚Üí S.C} :
    PermCond (Sum.elim c (Sum.elim c2 c3 ‚àò finSumFinEquiv.symm) ‚àò finSumFinEquiv.symm)
    (Sum.elim (Sum.elim c c2 ‚àò finSumFinEquiv.symm) c3 ‚àò finSumFinEquiv.symm)
    (prodAssocMap n1 n2 n3) := by
  apply And.intro
  ¬∑ simp only [prodAssocMap]
    change Function.Bijective (finCongr (by ring))
    exact (finCongr _).bijective
  ¬∑ intro i
    simp only [prodAssocMap, Function.comp_apply]
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    match i with
    | Sum.inl i =>
      obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
      match i with
      | Sum.inl i =>
        suffices Fin.cast (Nat.add_assoc n1 n2 n3)
          ((finSumFinEquiv (Sum.inl (finSumFinEquiv (Sum.inl i))))) =
          finSumFinEquiv (Sum.inl i) by {rw [this]; simp}
        simp [Fin.ext_iff]
      | Sum.inr i =>
        suffices Fin.cast (Nat.add_assoc n1 n2 n3)
          ((finSumFinEquiv (Sum.inl (finSumFinEquiv (Sum.inr i))))) =
          finSumFinEquiv (Sum.inr (finSumFinEquiv (Sum.inl i))) by {rw [this]; simp}
        simp [Fin.ext_iff]
    | Sum.inr i =>
      suffices Fin.cast (Nat.add_assoc n1 n2 n3) (finSumFinEquiv (Sum.inr i)) =
          finSumFinEquiv (Sum.inr (finSumFinEquiv (Sum.inr i))) by {rw [this]; simp}
      simp only [finSumFinEquiv_apply_right, Fin.ext_iff, Fin.coe_cast, Fin.coe_natAdd]
      exact Nat.add_assoc n1 n2 ‚Üëi

@[simp]
lemma prodAssocMap'_permCond {n1 n2 n3 : ‚Ñï} {c : Fin n1 ‚Üí S.C} {c2 : Fin n2 ‚Üí S.C}
    {c3 : Fin n3 ‚Üí S.C} : PermCond
      (Sum.elim (Sum.elim c c2 ‚àò finSumFinEquiv.symm) c3 ‚àò finSumFinEquiv.symm)
      (Sum.elim c (Sum.elim c2 c3 ‚àò finSumFinEquiv.symm) ‚àò finSumFinEquiv.symm)
      (prodAssocMap' n1 n2 n3) := by
  apply And.intro
  ¬∑ simp only [prodAssocMap']
    change Function.Bijective (finCongr (by ring))
    exact (finCongr _).bijective
  ¬∑ intro i
    simp only [prodAssocMap', Function.comp_apply]
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    match i with
    | Sum.inl i =>
      suffices ((Fin.cast (Nat.add_assoc n1 n2 n3).symm (finSumFinEquiv (Sum.inl i))))
        = finSumFinEquiv (Sum.inl (finSumFinEquiv (Sum.inl i))) by {rw [this]; simp}
      simp [Fin.ext_iff]
    | Sum.inr i =>
      obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
      match i with
      | Sum.inl i =>
        suffices (Fin.cast (Nat.add_assoc n1 n2 n3).symm
          (finSumFinEquiv (Sum.inr (finSumFinEquiv (Sum.inl i)))))
          = finSumFinEquiv (Sum.inl (finSumFinEquiv (Sum.inr i))) by {rw [this]; simp}
        simp [Fin.ext_iff]
      | Sum.inr i =>
        suffices (Fin.cast (Nat.add_assoc n1 n2 n3).symm
          (finSumFinEquiv (Sum.inr (finSumFinEquiv (Sum.inr i)))))
          = (finSumFinEquiv (Sum.inr i)) by {rw [this]; simp}
        simp only [finSumFinEquiv_apply_right, Fin.ext_iff, Fin.coe_cast, Fin.coe_natAdd]
        exact Eq.symm (Nat.add_assoc n1 n2 ‚Üëi)

/-!

## Relationships assocaited with products

-/

@[simp]
lemma Pure.prodP_equivariant {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (g : S.G) (p : Pure S c) (p1 : Pure S c1) :
    prodP (g ‚Ä¢ p) (g ‚Ä¢ p1) = g ‚Ä¢ prodP p p1 := by
  ext i
  obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
  conv_rhs =>
    rw [actionP_eq]
    simp only
  match i with
  | Sum.inl i =>
    simp only [finSumFinEquiv_apply_left, prodP_apply_castAdd]
    generalize_proofs h
    have h1 := (S.FD.map (eqToHom h)).comm g
    have h1' := congrFun (congrArg (fun x => x.hom) h1) (p i)
    simp only [Function.comp_apply, ModuleCat.hom_comp, Rep.œÅ_hom, LinearMap.coe_comp] at h1'
    exact h1'
  | Sum.inr i =>
    simp only [finSumFinEquiv_apply_right, prodP_apply_natAdd]
    generalize_proofs h
    have h1 := (S.FD.map (eqToHom h)).comm g
    have h1' := congrFun (congrArg (fun x => x.hom) h1) (p1 i)
    simp only [Function.comp_apply, ModuleCat.hom_comp, Rep.œÅ_hom, LinearMap.coe_comp] at h1'
    exact h1'

@[simp]
lemma prodT_equivariant {n1 n2} {c : Fin n1 ‚Üí S.C} {c1 : Fin n2 ‚Üí S.C}
    (g : S.G) (t : S.Tensor c) (t1 : S.Tensor c1) :
    prodT (g ‚Ä¢ t) (g ‚Ä¢ t1) = g ‚Ä¢ prodT t t1 := by
  let P (t : S.Tensor c) := prodT (g ‚Ä¢ t) (g ‚Ä¢ t1) = g ‚Ä¢ prodT t t1
  change P t
  apply induction_on_pure
  ¬∑ intro p
    let P (t1 : S.Tensor c1) := prodT (g ‚Ä¢ p.toTensor) (g ‚Ä¢ t1) = g ‚Ä¢ prodT p.toTensor t1
    change P t1
    apply induction_on_pure
    ¬∑ intro q
      simp only [P]
      rw [prodT_pure, actionT_pure, actionT_pure, prodT_pure, actionT_pure]
      simp
    ¬∑ intro r t h1
      simp_all only [actionT_smul, map_smul, P]
    ¬∑ intro t1 t2 h1 h2
      simp_all only [actionT_add, map_add, P]
  ¬∑ intro r t h1
    simp_all only [actionT_smul, map_smul, LinearMap.smul_apply, P]
  ¬∑ intro t1 t2 h1 h2
    simp_all only [actionT_add, map_add, LinearMap.add_apply, P]

lemma Pure.prodP_swap {n n1} {c : Fin n ‚Üí S.C}
    {c1 : Fin n1 ‚Üí S.C}
    (p : Pure S c) (p1 : Pure S c1) :
    Pure.prodP p p1 = permP (prodSwapMap n n1) (prodSwapMap_permCond) (Pure.prodP p1 p) := by
  ext i
  obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
  match i with
  | Sum.inl i =>
    simp only [finSumFinEquiv_apply_left, Function.comp_apply, prodP_apply_castAdd, permP]
    rw [‚Üê congr_right (p1.prodP p) _ (Fin.natAdd n1 i) (by simp [prodSwapMap])]
    simp [map_map_apply]
  | Sum.inr i =>
    simp only [finSumFinEquiv_apply_right, Function.comp_apply, prodP_apply_natAdd, permP]
    rw [‚Üê congr_right (p1.prodP p) _ (Fin.castAdd n i) (by simp [prodSwapMap])]
    simp [map_map_apply]

lemma prodT_swap {n n1} {c : Fin n ‚Üí S.C}
    {c1 : Fin n1 ‚Üí S.C}
    (t : S.Tensor c) (t1 : S.Tensor c1) :
    prodT t t1 = permT (prodSwapMap n n1) (prodSwapMap_permCond) (prodT t1 t) := by
  let P (t : S.Tensor c) := prodT t t1 = permT (prodSwapMap n n1) (prodSwapMap_permCond)
    (prodT t1 t)
  change P t
  apply induction_on_pure
  ¬∑ intro p
    let P (t1 : S.Tensor c1) := prodT p.toTensor t1 = permT (prodSwapMap n n1)
      (prodSwapMap_permCond) (prodT t1 p.toTensor)
    change P t1
    apply induction_on_pure
    ¬∑ intro q
      simp only [P]
      rw [prodT_pure, prodT_pure, permT_pure, Pure.prodP_swap]
    ¬∑ intro r t h1
      simp_all only [map_smul, LinearMap.smul_apply, P]
    ¬∑ intro t1 t2 h1 h2
      simp_all only [map_add, LinearMap.add_apply, P]
  ¬∑ intro r t h1
    simp_all only [actionT_smul, map_smul, LinearMap.smul_apply, P]
  ¬∑ intro t1 t2 h1 h2
    simp_all only [actionT_add, map_add, LinearMap.add_apply, P]

@[simp]
lemma Pure.prodP_permP_left {n n'} {c : Fin n ‚Üí S.C} {c' : Fin n' ‚Üí S.C}
    (œÉ : Fin n' ‚Üí Fin n) (h : PermCond c c' œÉ) (p : Pure S c) (p2 : Pure S c2) :
    Pure.prodP (permP œÉ h p) p2 = permP (prodLeftMap n2 œÉ)
      (prodLeftMap_permCond c2 h) (Pure.prodP p p2) := by
  funext i
  obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
  match i with
  | Sum.inl i =>
    simp only [permP, prodLeftMap, Pure.prodP_apply_castAdd]
    simp only [Function.comp_apply]
    have h1 := congr_right (p.prodP p2)
      (finSumFinEquiv (Sum.map œÉ id (finSumFinEquiv.symm (finSumFinEquiv (Sum.inl i)))))
      (finSumFinEquiv (Sum.inl (œÉ i)))
      (by simp)
    rw [‚Üê h1]
    simp [finSumFinEquiv_apply_left, prodP_apply_castAdd, Function.comp_apply, permP,
      map_map_apply]
  | Sum.inr i =>
    simp only [permP, prodLeftMap, Pure.prodP_apply_natAdd]
    simp only [Function.comp_apply]
    have h1 := congr_right (p.prodP p2)
      (finSumFinEquiv (Sum.map œÉ id (finSumFinEquiv.symm (finSumFinEquiv (Sum.inr i)))))
      (finSumFinEquiv (Sum.inr i))
      (by simp)
    rw [‚Üê h1]
    simp [permP, map_map_apply]

@[simp]
lemma prodT_permT_left {n n'} {c : Fin n ‚Üí S.C} {c' : Fin n' ‚Üí S.C}
    (œÉ : Fin n' ‚Üí Fin n) (h : PermCond c c' œÉ) (t : S.Tensor c) (t2 : S.Tensor c2) :
    prodT (permT œÉ h t) t2 = permT (prodLeftMap n2 œÉ) (prodLeftMap_permCond c2 h) (prodT t t2) := by
  let P (t : S.Tensor c) := prodT (permT œÉ h t) t2 =
    permT (prodLeftMap n2 œÉ) (prodLeftMap_permCond c2 h) (prodT t t2)
  change P t
  apply induction_on_pure
  ¬∑ intro p
    let P (t2 : S.Tensor c2) := prodT (permT œÉ h p.toTensor) t2 =
      permT (prodLeftMap n2 œÉ) (prodLeftMap_permCond c2 h) (prodT p.toTensor t2)
    change P t2
    apply induction_on_pure
    ¬∑ intro q
      simp only [P]
      rw [prodT_pure, permT_pure, permT_pure, prodT_pure]
      congr
      simp
    ¬∑ intro r t h1
      simp_all only [actionT_smul, map_smul, P]
    ¬∑ intro t1 t2 h1 h2
      simp_all only [actionT_add, map_add, P]
  ¬∑ intro r t h1
    simp_all only [actionT_smul, map_smul, LinearMap.smul_apply, P]
  ¬∑ intro t1 t2 h1 h2
    simp_all only [actionT_add, map_add, LinearMap.add_apply, P]

@[simp]
lemma Pure.prodP_permP_right {n n'} {c : Fin n ‚Üí S.C} {c' : Fin n' ‚Üí S.C}
    (œÉ : Fin n' ‚Üí Fin n) (h : PermCond c c' œÉ) (p : Pure S c) (p2 : Pure S c2) :
    prodP p2 (permP œÉ h p) = permP (prodRightMap n2 œÉ)
      (prodRightMap_permCond c2 h) (Pure.prodP p2 p) := by
  conv_lhs => rw [prodP_swap]
  conv_rhs => rw [prodP_swap]
  simp only [prodP_permP_left, prodSwapMap_permCond, permP_permP]
  apply Pure.permP_congr
  ¬∑ ext i
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    simp only [prodLeftMap, prodSwapMap, Function.comp_apply, Equiv.symm_apply_apply, prodRightMap]
    match i with
    | Sum.inl i => rfl
    | Sum.inr i => rfl
  ¬∑ rfl

@[simp]
lemma prodT_permT_right {n n'} {c : Fin n ‚Üí S.C} {c' : Fin n' ‚Üí S.C}
    (œÉ : Fin n' ‚Üí Fin n) (h : PermCond c c' œÉ) (t : S.Tensor c) (t2 : S.Tensor c2) :
    prodT t2 (permT œÉ h t) = permT (prodRightMap n2 œÉ)
    (prodRightMap_permCond c2 h) (prodT t2 t) := by
  conv_lhs => rw [prodT_swap]
  conv_rhs => rw [prodT_swap]
  simp only [prodT_permT_left, prodSwapMap_permCond, permT_permT]
  apply permT_congr
  ¬∑ ext i
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    simp only [prodLeftMap, prodSwapMap, Function.comp_apply, Equiv.symm_apply_apply, prodRightMap]
    match i with
    | Sum.inl i => rfl
    | Sum.inr i => rfl
  ¬∑ rfl

lemma Pure.prodP_assoc {n n1 n2} {c : Fin n ‚Üí S.C}
    {c1 : Fin n1 ‚Üí S.C} {c2 : Fin n2 ‚Üí S.C}
    (p : Pure S c) (p1 : Pure S c1) (p2 : Pure S c2) :
    prodP (Pure.prodP p p1) p2 =
    permP (prodAssocMap n n1 n2) (prodAssocMap_permCond) (Pure.prodP p (Pure.prodP p1 p2)) := by
  ext i
  obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
  match i with
  | Sum.inl i =>
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    match i with
    | Sum.inl i =>
      simp only [finSumFinEquiv_apply_left, Function.comp_apply, prodP_apply_castAdd, permP]
      rw [‚Üê congr_right (p.prodP (p1.prodP p2)) _ _ (prodAssocMap_castAdd_castAdd i)]
      simp [map_map_apply, - eqToHom_refl, - Discrete.functor_map_id]
    | Sum.inr i =>
      simp only [finSumFinEquiv_apply_right, finSumFinEquiv_apply_left, Function.comp_apply,
        prodP_apply_castAdd, prodP_apply_natAdd, permP]
      rw [‚Üê congr_right (p.prodP (p1.prodP p2)) _ _ (prodAssocMap_castAdd_natAdd i)]
      simp [map_map_apply, - eqToHom_refl, - Discrete.functor_map_id]
  | Sum.inr i =>
    simp only [finSumFinEquiv_apply_right, Function.comp_apply, prodP_apply_natAdd, permP]
    rw [‚Üê congr_right (p.prodP (p1.prodP p2)) _ _ (prodAssocMap_natAdd i)]
    simp [map_map_apply]

lemma prodT_assoc {n n1 n2} {c : Fin n ‚Üí S.C}
    {c1 : Fin n1 ‚Üí S.C} {c2 : Fin n2 ‚Üí S.C}
    (t : S.Tensor c) (t1 : S.Tensor c1) (t2 : S.Tensor c2) :
    prodT (prodT t t1) t2 =
    permT (prodAssocMap n n1 n2) (prodAssocMap_permCond) (prodT t (prodT t1 t2)) := by
  let P (t : S.Tensor c) (t1 : S.Tensor c1) (t2 : S.Tensor c2) := prodT (prodT t t1) t2 =
    permT (prodAssocMap n n1 n2) (prodAssocMap_permCond) (prodT t (prodT t1 t2))
  let P1 (t : S.Tensor c) := P t t1 t2
  change P1 t
  refine induction_on_pure ?_
    (fun r t h1 => by simp_all only [map_smul, LinearMap.smul_apply, prodAssocMap_permCond, P1, P])
    (fun t1 t2 h1 h2 => by
      simp_all only [map_add, LinearMap.add_apply, prodAssocMap_permCond, P1, P]) t
  intro p
  let P2 (t1 : S.Tensor c1) := P p.toTensor t1 t2
  change P2 t1
  refine induction_on_pure ?_
    (fun r t h1 => by
      simp_all only [map_smul, LinearMap.smul_apply, prodAssocMap_permCond, P2, P])
    (fun t1 t2 h1 h2 => by
      simp_all only [map_add, LinearMap.add_apply, prodAssocMap_permCond, P2, P]) t1
  intro p1
  let P3 (t2 : S.Tensor c2) := P p.toTensor p1.toTensor t2
  change P3 t2
  refine induction_on_pure ?_
    (fun r t h1 => by
      simp_all only [map_smul, LinearMap.smul_apply, prodAssocMap_permCond, P3, P])
    (fun t1 t2 h1 h2 => by
      simp_all only [map_add, LinearMap.add_apply, prodAssocMap_permCond, P3, P]) t2
  intro p2
  simp only [P3, P, P2, P1]
  rw [prodT_pure, prodT_pure, prodT_pure, prodT_pure, permT_pure, Pure.prodP_assoc]

lemma Pure.prodP_assoc' {n n1 n2} {c : Fin n ‚Üí S.C}
    {c1 : Fin n1 ‚Üí S.C} {c2 : Fin n2 ‚Üí S.C}
    (p : Pure S c) (p1 : Pure S c1) (p2 : Pure S c2) :
    prodP p (prodP p1 p2) =
    permP (prodAssocMap' n n1 n2) (prodAssocMap'_permCond) (prodP (prodP p p1) p2) := by
  ext i
  obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
  match i with
  | Sum.inl i =>
    simp only [finSumFinEquiv_apply_left, Function.comp_apply, prodP_apply_castAdd, permP]
    rw [‚Üê congr_right ((p.prodP p1).prodP p2) _ _ (prodAssocMap'_castAdd i)]
    simp [map_map_apply, - eqToHom_refl, - Discrete.functor_map_id]
  | Sum.inr i =>
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    match i with
    | Sum.inl i =>
      simp only [finSumFinEquiv_apply_left, finSumFinEquiv_apply_right, Function.comp_apply,
        prodP_apply_natAdd, prodP_apply_castAdd, permP]
      rw [‚Üê congr_right ((p.prodP p1).prodP p2) _ _ (prodAssocMap'_natAdd_castAdd i)]
      simp [map_map_apply, - eqToHom_refl, - Discrete.functor_map_id]
    | Sum.inr i =>
      simp only [finSumFinEquiv_apply_right, Function.comp_apply, prodP_apply_natAdd, permP]
      rw [‚Üê congr_right ((p.prodP p1).prodP p2) _ _ (prodAssocMap'_natAdd_natAdd i)]
      simp [map_map_apply]

lemma prodT_assoc' {n n1 n2} {c : Fin n ‚Üí S.C}
    {c1 : Fin n1 ‚Üí S.C} {c2 : Fin n2 ‚Üí S.C}
    (t : S.Tensor c) (t1 : S.Tensor c1) (t2 : S.Tensor c2) :
    prodT t (prodT t1 t2) =
    permT (prodAssocMap' n n1 n2) (prodAssocMap'_permCond) (prodT (prodT t t1) t2) := by
  let P (t : S.Tensor c) (t1 : S.Tensor c1) (t2 : S.Tensor c2) := prodT t (prodT t1 t2) =
    permT (prodAssocMap' n n1 n2) (prodAssocMap'_permCond) (prodT (prodT t t1) t2)
  let P1 (t : S.Tensor c) := P t t1 t2
  change P1 t
  refine induction_on_pure ?_
    (fun r t h1 => by simp_all only [map_smul, LinearMap.smul_apply, prodAssocMap'_permCond, P1, P])
    (fun t1 t2 h1 h2 => by
      simp_all only [map_add, LinearMap.add_apply, prodAssocMap'_permCond, P1, P]) t
  intro p
  let P2 (t1 : S.Tensor c1) := P p.toTensor t1 t2
  change P2 t1
  refine induction_on_pure ?_
    (fun r t h1 => by
      simp_all only [map_smul, LinearMap.smul_apply, prodAssocMap'_permCond, P2, P])
    (fun t1 t2 h1 h2 => by
      simp_all only [map_add, LinearMap.add_apply, prodAssocMap'_permCond, P2, P]) t1
  intro p1
  let P3 (t2 : S.Tensor c2) := P p.toTensor p1.toTensor t2
  change P3 t2
  refine induction_on_pure ?_
    (fun r t h1 => by
      simp_all only [map_smul, LinearMap.smul_apply, prodAssocMap'_permCond, P3, P])
    (fun t1 t2 h1 h2 => by
      simp_all only [map_add, LinearMap.add_apply, prodAssocMap'_permCond, P3, P]) t2
  intro p2
  simp only [P3, P, P2, P1]
  rw [prodT_pure, prodT_pure, prodT_pure, prodT_pure, permT_pure, Pure.prodP_assoc']

end Tensor

end TensorSpecies
